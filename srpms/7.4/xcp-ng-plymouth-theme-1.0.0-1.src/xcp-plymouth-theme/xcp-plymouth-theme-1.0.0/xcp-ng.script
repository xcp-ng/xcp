# This is the XCP-ng plymouth theme script

Window.SetBackgroundTopColor(0.234, 0.43, 0.705);
Window.SetBackgroundBottomColor(0.16, 0.25, 0.44);

logo_image = Image("background.png");
logo.opacity_angle = 0;
big_logo = logo_image.Scale (Window.GetWidth() , Window.GetHeight ());
logo.sprite = Sprite(big_logo);

progress_box.image = Image("progress_box.png");
progress_box.sprite = Sprite(progress_box.image);

progress_box.x = Window.GetX() + Window.GetWidth() / 2 - progress_box.image.GetWidth() / 2;
progress_box.y = Window.GetY() + Window.GetHeight() /2 * 1.25 - progress_box.image.GetHeight() / 2;
progress_box.sprite.SetPosition(progress_box.x, progress_box.y, 0);

progress_bar.original_image = Image("progress_bar.png");
progress_bar.sprite = Sprite();

progress_bar.x = Window.GetX() + Window.GetWidth()  / 2 -        progress_bar.original_image.GetWidth() / 2;
progress_bar.y = Window.GetY() + Window.GetHeight() / 2 * 1.25  - progress_box.image.GetHeight() / 2 + (progress_box.image.GetHeight() - progress_bar.original_image.GetHeight()) / 2;
progress_bar.sprite.SetPosition(progress_bar.x, progress_bar.y, 1);

fun progress_callback (duration, progress)
  {

    if (Plymouth.GetMode()=="shutdown" || mode=="suspend")
        progress = 1-progress;

    if (progress_bar.image.GetWidth () != Math.Int (progress_bar.original_image.GetWidth () * progress))
      {
        progress_bar.image = progress_bar.original_image.Scale(progress_bar.original_image.GetWidth(progress_bar.original_image) * progress, progress_bar.original_image.GetHeight());
        progress_bar.sprite.SetImage (progress_bar.image);
      }
  }

Plymouth.SetBootProgressFunction(progress_callback);

#----------------------------------------- Quit --------------------------------

fun quit_callback ()
{
  logo.sprite.SetOpacity (1);
}

Plymouth.SetQuitFunction(quit_callback);

#----------------------------------------- Message --------------------------------

message_sprites = [];
message_sprite_count = 0;
message_sprite_y = 0;

fun display_message_callback (text)
{
  my_image = Image.Text(text, 0.078, 0.28, 0.482, "FreeSans");
  message_sprites[message_sprite_count] = Sprite(my_image);

  message_x = (Window.GetWidth() /2) - (my_image.GetWidth() /2);

  message_sprites[message_sprite_count].SetPosition(message_x, message_sprite_y+(Window.GetHeight() * 0.7), 10000);
  message_sprites[message_sprite_count].text = text;


  if (message_sprite_count==8) {
    for (i = 0; i < 8; i++)
      {
      message_sprites[i]=message_sprites[i+1];
      message_sprites[i].SetPosition( message_sprites[i].GetX(), i*my_image.GetHeight() +(Window.GetHeight() * 0.7), 10000);
      }
    } else {
      message_sprite_count++;
      message_sprite_y += my_image.GetHeight();
    }

}

fun hide_message_callback (text)
{
  for (i = 0; i < message_sprite_count; i++)
    {
      if (message_sprites[i].text == text)
        message_sprites[i] = NULL;
    }
}

Plymouth.SetDisplayMessageFunction (display_message_callback);
Plymouth.SetHideMessageFunction (hide_message_callback);

Plymouth.SetUpdateStatusFunction(dispay_message_callback);


